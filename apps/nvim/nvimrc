" Based on dotfiles from @viccuad on GitHub - <3

" Get plugin manager if it doesn't exist
if empty(glob('~/.vim/autoload/plug.vim'))
		silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
		\ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
		autocmd VimEnter * PlugInstall
endif

" TODO: Auto-detect OS?

" For Mac/Linux users
call plug#begin('~/.vim/bundle')

" For Windows users
" call plug#begin('~/vimfiles/bundle')

" Pretty status line
Plug 'bling/vim-airline'
	let g:airline_powerline_fonts = 0
	let g:airline#extensions#tabline#fnamemod = ':t'
	set laststatus=2 " Always show statusline
	set noshowmode " Hides default mode
	let g:airline#extensions#tabline#enabled = 1 " Automatically displays all buffers when there's only one tab open

  " If you don't have powerline fonts, use the following
	let g:airline#extensions#tabline#left_sep = '' " straight separators
	let g:airline#extensions#tabline#left_alt_sep = ''

  let g:airline_right_alt_sep = ''
  let g:airline_right_sep = ''
  let g:airline_left_alt_sep= ''
  let g:airline_left_sep = ''

	"let g:airline#extensions#tabline#buffer_idx_mode = 1	" display numbers in the tab line, and use mappings <leader>1 to <leader>9

" Plugins that make text-wizarding much more fun
"Plug 'majutsushi/tagbar' " shows a list of tags (requires ctags)
Plug 'tpope/vim-surround'
Plug 'tpope/vim-speeddating'
Plug 'tpope/vim-fugitive'
Plug 'airblade/vim-gitgutter'
Plug 'kien/ctrlp.vim'
Plug 'nathanaelkane/vim-indent-guides'
Plug 'scrooloose/syntastic'
Plug 'LargeFile'
Plug 'tpope/vim-commentary'
Plug 'junegunn/vim-easy-align'
Plug 'tmux-plugins/vim-tmux-focus-events'
	let g:NERDSpaceDelims = 1

Plug 'tasklist.vim' " <leader>, t will show TODOs and FIXMEs
Plug 'christoomey/vim-tmux-navigator'

Plug 'mhinz/vim-startify' " a start screen with recently modified files and vim sessions
	let g:startify_session_persistence = 1 " automatically update sessions
	let g:startify_session_delete_buffers = 1 " delete open buffers before loading a new session
	let g:startify_custom_footer = [
		\ '',
		\ '    b   ➤ open in new buffer  :SLoad   ➤ load a session     ',
		\ '    s,v ➤ open in split       :SSave   ➤ save a session     ',
		\ '    t   ➤ open in tab         :SDelete ➤ delete a session   ',
		\ '',
		\ ]
	let g:startify_custom_header =
		\ map(split(system('fortune'), '\n'), '"   ". v:val') + ['']

Plug 'jez/vim-superman'
	noremap K :SuperMan <cword><CR>

" Plugins for code completion and snippets
Plug 'Valloric/YouCompleteMe', {'do': './install.sh --clang-completer'}		" (NEEDS to be compiled, read the docs!)
	let g:ycm_global_ycm_extra_conf = '~/.vim/bundle/YouCompleteMe/third_party/ycmd/cpp/ycm/.ycm_extra_conf.py'

Plug 'SirVer/ultisnips'
Plug 'honza/vim-snippets'
	let g:UltiSnipsSnippetDirectories=["snippets_UltiSnips"]
	function! g:UltiSnips_Complete()
		call UltiSnips#ExpandSnippet()
		if g:ulti_expand_res == 0
			if pumvisible()
				return "\<C-n>"
			else
				call UltiSnips#JumpForwards()
				if g:ulti_jump_forwards_res == 0
					return "\<TAB>"
				endif
			endif
		endif
		return ""
	endfunction
	au BufEnter * exec "inoremap <silent> " . g:UltiSnipsExpandTrigger . " <C-R>=g:UltiSnips_Complete()<cr>"
	let g:UltiSnipsJumpForwardTrigger="<tab>"
	let g:UltiSnipsListSnippets="<c-e>"

	" this maps Enter key to <C-y> to chose the current highlight item
	" and close the selection list, same as other IDEs:
	" CONFLICTS with some plugins like tpope/Endwise
	inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"

" Filetype handling
Plug 'kchmck/vim-coffee-script'
Plug 'wavded/vim-stylus'
Plug 'rust-lang/rust.vim'
Plug 'godlygeek/tabular'
Plug 'plasticboy/vim-markdown'
Plug 'justinmk/vim-syntax-extra', {'for': 'c'}
Plug 'freitass/todo.txt-vim', {'for': 'todo'}
Plug 'vivien/vim-addon-linux-coding-style', {'for': 'c'}

Plug 'c.vim', {'for': 'c'}
	let g:C_LocalTemplateFile = $HOME.'/.vim/snippets_Cvim/c-support/templates/Templates' " this allows for the templates to be versioned on .dotfiles

Plug 'hdima/python-syntax', {'for': 'python'}
	let python_highlight_all = 1
	" you can change between py v2 and v3 with :Python2Syntax and :Python3Syntax

call plug#end()
" }}}

" Filetype & languages {{{
filetype on
filetype indent on
filetype plugin on

" C language
let c_space_errors = 1
let c_comment_strings = 0		" dont highlight strings inside C comments

" Python language
let python_space_errors = 1
autocmd FileType python setl tabstop=2 expandtab shiftwidth=2 softtabstop=2

" HamlC
autocmd BufRead,BufNewFile *.hamlc set ft=haml

" Java language
let java_space_errors = 1

" Markdown instead of modula2
" autocmd BufNewFile,BufReadPost *.md setl filetype=markdown spell textwidth=0 wrapmargin=0

" TXT files
" autocmd BufNewFile,BufReadPost *.txt setl spell textwidth=0 wrapmargin=0

" Mail files from mutt
" autocmd FileType mail setl nonumber spell textwidth=0 wrapmargin=0
" }}}

" Spaces and tabs {{{
set tabstop=2
set shiftwidth=2
set softtabstop=2
set expandtab
set autoindent smartindent
set listchars=tab:\ \ ,trail:·
set list
" }}}

" Line wrap {{{
"let &colorcolumn=join(range(81,400),",") " colors columns past 80
set wrap
set linebreak
set breakindent
set textwidth=80
set formatoptions=tcrql1j
  " t autowrap to textwidth
  " c autowrap comments to textwidth
  " r autoinsert comment leader with <enter>
  " q allow formatting of comments with gq
  " l	Long lines are not broken in insert mode: When a line was longer than 'textwidth' when the insert command started, Vim does not automatically format it.
  " 1	Don't break a line after a one-letter word. It's broken before it instead (if possible).
  " j	Where it makes sense, remove a comment leader when joining lines
" }}}

" Look and feel {{{
syntax on
set title
set synmaxcol=2048
set number
"set relativenumber
set cursorline
"set cursorcolumn
set showcmd
set nowildmenu
set wildmode=longest,list,full
set cpoptions-=$
set showmatch
set mouse=a
set mousehide
set backspace=indent,eol,start
set ruler
set lazyredraw
set scrolloff=8
set sidescrolloff=8
set splitright
set splitbelow
set noerrorbells
set visualbell
set vb t_vb=
set nobackup
set nowritebackup
set noswapfile
if has('autocmd')
	autocmd GUIEnter * set visualbell t_vb=
endif
set timeout
set ttimeoutlen=200

if has("gui_running")
	set guiheadroom=0
	set background=dark

	set guioptions-=T
	set guioptions+=c
	set guioptions-=r
	set guioptions-=R
	set guioptions-=l
	set guioptions-=L

	if has("gui_gtk2")
		set guifont=DejaVu\ Sans\ Mono\ for\ Powerline\ 8
	elseif has("gui_macvim")
		set guifont=Menlo\ Regular:h14
	elseif has("gui_win32")
		set guifont=Consolas:h11:cANSI
	endif
else
	set t_Co=256
	let base16colorspace=256
	set background=dark
	colorscheme current
	highlight ColorColumn ctermbg=235 guibg=#262626
endif

highlight clear SignColumn

if &term == 'xterm-256color' || &term == 'screen-256color'
	let &t_SI = "\<Esc>[5 q"
	let &t_EI = "\<Esc>[1 q"
endif

if &term == 'rxvt-unicode-256color'
	let &t_SI = "\<Esc>[3 q"
	let &t_EI = "\<Esc>[1 q"
endif
" }}}

" Persistence {{{
set viminfo+=%
set viminfo+=n$HOME/.vim/.viminfo

" No empty buffer on startup
autocmd VimEnter * nested if  bufname('')==''  &&  line('$') == 1 &&  col('$')==1 &&  !&modified | bd % | endif

set hidden
set history=1000
set undofile
set undodir=$HOME/.vim/undo
set undolevels=1000
set undoreload=10000
" }}}

" Backup and Swap files {{{
set backupdir=$HOME/.vim/backup
set directory=$HOME/.vim/backup
" }}}

" Searching {{{
set ignorecase
set smartcase
set incsearch
set hlsearch
set wrapscan
" }}}

" Mappings {{{
" make these commonly mistyped commands still work:
command! WQ wq
command! Wq wq
command! Wqa wqa
command! W w
command! Q q

let mapleader = "\<Space>"

" use tab key to cycle through the buffers:
nnoremap <leader><Tab>   :bnext<CR>
nnoremap <leader><S-Tab> :bprevious<CR>

" don't kill vim
nnoremap <leader>K <Nop>

" quick paragraph formatting
vmap Q gq
nmap Q gqap

" use ctrl j and k to switch buffers as well
nnoremap <leader>k :bnext<CR>
nnoremap <leader>j :bprevious<CR>

" fast word change
nnoremap <leader>c ciw

" bash-like deletion
inoremap <C-BS> <C-w>
inoremap <A-BS> <C-w>

" clear search higlight
nnoremap <leader>/ :let @/ = ""<CR>

" remap jk to escape in insert mode:  You'll never type jk anyway, so it's great!
inoremap jk <Esc>
inoremap jj <Esc>

" use hjkl-movement between rows when soft wrapping:
nnoremap j gj
nnoremap k gk
vnoremap j gj
vnoremap k gk

" force yourself to stop using the arrow keys:
map <up>    <nop>
map <down>  <nop>
map <left>  <nop>
map <right> <nop>

" force yourself to use jj or jk
inoremap <Esc> <nop>

" move between tabs:
nmap <leader>1 <Plug>AirlineSelectTab1
nmap <leader>2 <Plug>AirlineSelectTab2
nmap <leader>3 <Plug>AirlineSelectTab3
nmap <leader>4 <Plug>AirlineSelectTab4
nmap <leader>5 <Plug>AirlineSelectTab5
nmap <leader>6 <Plug>AirlineSelectTab6
nmap <leader>7 <Plug>AirlineSelectTab7
nmap <leader>8 <Plug>AirlineSelectTab8
nmap <leader>9 <Plug>AirlineSelectTab9

" list of shared binds:
" write file without confirmation:
map <silent> <F2> :write<CR>

" remove trailing whitespace:
map <F3> :%s/\s\+$//<CR>:let @/ = ""<CR>

" close buffer:
nnoremap <silent> <leader>w <Esc>:bd<CR>
map <silent> <F4> <Esc>:bd<CR>

" toggle spell checking:
map <F5> :setlocal spell!<CR>

" open urls, files, etc. example: http://google.com:
noremap <leader>o :!xdg-open <cfile><CR><CR>
set isfname+=32

" previous c error (c.vim plugin):
map  <silent> <F7>    <Esc>:cprevious<CR>

" next c error (c.vim plugin)
map  <silent> <F8>    <Esc>:cnext<CR>

" keep that dumb window from popping up
map q: :q

" compile & link c code (alt+<F9> write + compile, ctrl+<F9> compile + run) (c.vim plugin)
"<F9>
" }}}

" Folding {{{
set foldmethod=syntax
set foldlevel=99
set foldnestmax=10
set nofoldenable
set foldlevelstart=1
" }}}

" Launch {{{
set encoding=utf8
set autowrite
set autochdir
"autocmd BufEnter * silent! lcd %:p:h		" automatically cd into the dir of the file. this breaks less
set autoread
set nomodeline

" jump to the last position when reopening a file:
if has("autocmd")
	au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
endif
" }}}

" vim:foldmethod=marker:foldlevel=0
